apply plugin: "war"
apply plugin: "jetty"

dependencies {
    compile "javax.servlet:servlet-api:2.5"
}

httpPort = 8080
stopPort = 9090
stopKey = "stopKey"

configurations {
    runtimeOnly
}

dependencies {
    // to run our App on Heroku
    runtimeOnly "org.mortbay.jetty:jetty-runner:8.1.1.v20120215"
    // monitoring on Heroku
    runtimeOnly "com.newrelic.agent.java:newrelic-agent:2.18.0"
    compile project(':core')
}

task copyToLib << {
    copy {
        from configurations.runtimeOnly.copy().setTransitive(false)
        into "$buildDir/libs"
        rename { name ->
            def artifacts = configurations.runtimeOnly.resolvedConfiguration.resolvedArtifacts
            def artifact = artifacts.find { it.file.name == name }
            "${artifact.name}.${artifact.extension}"
        }
    }
}

task stage(dependsOn: ["clean", "war", "copyToLib"])